on run argv
  set image_name to item 1 of argv

  try
    tell application "Finder"
      -- Reference the disk image
      tell disk image_name
        -- Wait for the image to finish mounting
        set open_attempts to 0
        repeat while open_attempts < 4
          try
            open
            delay 1
            set open_attempts to 5
          on error errStr number errorNumber
            set open_attempts to open_attempts + 1
            log "Error during open attempt: " & errStr
            delay 10
          end try
        end repeat
        delay 5

        -- Open the image the first time and save a DS_Store with background and icon setup
        open
        set current view of container window to icon view
        set theViewOptions to the icon view options of container window
        set background picture of theViewOptions to file ".background:background.tif"
        set arrangement of theViewOptions to not arranged
        set icon size of theViewOptions to 128
        delay 5
        close

        -- Setup the position of the app and Applications symlink
        open
        update without registering applications
        tell container window
          set sidebar width to 0
          set statusbar visible to false
          set toolbar visible to false
          set the bounds to {400, 100, 900, 465}
          set position of item "@f3d_appname@" to {103, 150}
          set position of item "Applications" to {408, 150}
        end tell
        update without registering applications
        delay 5
        close

        -- Final open and close to verify setup
        open
        delay 5
        close
      end tell
    end tell
  on error errMsg
    log "Script failed with error: " & errMsg
  end try
end run
